/*
 * rt3 subproject (model interpreter runtime to test our ideas).
 */

plugins {
    id 'java-library'
}


dependencies {
    implementation 'org.ow2.asm:asm:9.0'

    // JUnit 5 dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}


def genOutputDir = "$buildDir/generated-src/gen"
def genJavaDir = "$genOutputDir/java/main"
def genTestJavaDir = "$genOutputDir/java/test"


sourceSets {

    main {
        java {
            srcDir genJavaDir
        }
    }

    test {
        java {
            srcDir genTestJavaDir
        }
    }
}


class PythonGenerate extends Exec {

    PythonGenerate() {
        executable 'python'
        environment('PYTHONPATH', getProject().file('tools/codegen'))
    }
}


test {
    useJUnitPlatform {  // JUnit5 as standard
        includeEngines 'junit-jupiter'
    }
}


task generateJava(type: PythonGenerate,
        description: 'Generate ...' ) {

    def outDir = relativePath("$genJavaDir/uk/co/farowl/vsj3/evo1")

    doFirst {
        mkdir outDir
    }
}
//compileJava.dependsOn(generateJava)


task pythonVersion(type: Exec) {
    executable 'python'
    args '-V'
}




// Some adjustments to Javadoc to ensure complex tables readable

tasks.withType(Javadoc) {

    options.showFromPackage()

    // addStringOption inserts one "-" for us :/
    // CSS adjustments (initially only for table style)
    options.addStringOption("-add-stylesheet",
        "src/main/javadoc/project-styles.css")

    // Enable "custom" tags used in JDK Javadoc since JSR-335.
    // https://nipafx.dev/javadoc-tags-apiNote-implSpec-implNote
    options.tags(
        "apiNote:a:API Note:",
        "implSpec:a:Implementation Requirements:",
        "implNote:a:Implementation Note:",
        // Unfortunately we must add these standard tags too,
        // so they come after the ones we want to enable.
        "param", "return", "throws",
        "since", "version", "serialData", "see")
}


tasks.withType(JavaCompile) {

    options.deprecation = true
}
